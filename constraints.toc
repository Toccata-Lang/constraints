
;; TODO: don't need this after hash-map literals are properly encoded
(def ListType (inline C Integer "(Value *)&(Integer){IntegerType, -1, ListType};"))
(def VectorType (inline C Integer "(Value *)&(Integer){IntegerType, -1, VectorType};"))
(def IntegerType (inline C Integer "(Value *)&(Integer){IntegerType, -1, IntegerType};"))
(def SymbolType (inline C Integer "(Value *)&(Integer){IntegerType, -1, SymbolType};"))

(defprotocol Constraint
  (assertion-path [x])

  (format-path [x n])

  (intersect [c1 c2])

  (restrict-types [c type-map]
    type-map)

  (extract-type-map [_]
    (assert-result r (instance? Maybe r)))

  (extract-items-constraints [_]
    (assert-result r (instance? Vector r))
    [])

  (extract-inner-constraint [_])

  (update-path [constraint file-name line-number]
    constraint)

  (update-sym [tc new-sym]
    (assert (instance? Tagged new-sym)))

  (update-var [tc new-var]
    (assert (instance? String new-var)))

  (satisfied-by [c info]
    (assert-result r (instance? Maybe r))
    ;; TODO: the whole idea behind this function is suspect
    nothing))

;; TODO: constraints to add
;; min count (instead of ItemsConstraint)
;; min value
;; keys required in a hash map
;; key constraint on hash maps

(defn format-path* [path desired-len]
  (either (empty? path)
          (let [path (-> path
                         (remove (fn [[file line]]
                                   (either (and (instance? Symbol file)
                                                (maybe nothing))
                                           (or (empty? file)
                                               (= 0 line)))))
                         (map (fn [[file line]]
                                (str file ": " line))))
                max-width (reduce path 0 (fn [max s]
                                           (let [n (count s)]
                                             (either (> max n)
                                                     n))))
                path (either (and (< (count path) desired-len)
                                  (maybe (comp path (repeat (- desired-len (count path))
                                                            (spaces max-width)))))
                             path)]
            (map path
                 (fn [x]
                   (str x (spaces (- max-width (count x)))))))))

(deftype NoValues [constraints]
  Stringable
  (string-list [_]
    (list "<NoValues \n  " (to-str (interpose constraints "\n  ")) ">"))

  Constraint
  (intersect [nv _] nv)
  (update-sym [x _] x)
  (update-var [x _] x)

  (format-path [_ n]
    (let [max-len (reduce (map constraints (fn [c] (count (assertion-path c))))
                          n
                          (fn [max-len path-len]
                            (either (> max-len path-len)
                                    path-len)))
          paths (map constraints (fn [c]
                                   (format-path c (inc max-len))))]
      (-> paths
          (list-zipper empty-list)
          (map (fn [strs]
                 (str (to-str (interpose strs "    "))
                      "\n"))))))

  Eq
  (=* [x y]
    (and (=* (get-type x) (get-type y))
         (or (and (< (count (.constraints x)) (count (.constraints y)))
                  (maybe y))
             (maybe x)))))

(deftype AllValues []
  Stringable
  (string-list [_]
    (list "<AllValues>"))

  Eq
  (=* [x y]
    (and (=* (get-type x) (get-type y))
         (maybe x)))

  Constraint
  (intersect [_ c] c)
  (update-sym [x _] x)

  (update-var [x _] x)
  (extract-type-map [_] nothing))

(def bottom-type (NoValues []))
(def top-type (AllValues))

(deftype MultiConstraint [constraints]
  (assert (instance? Vector constraints))

  Stringable
  (string-list [_] (list "<MultiConstraint \n" (to-str (interpose constraints "\n-- ")) ">"))

  Eq
  (=* [x y]
    (and (=* (get-type x) (get-type y))
         (= constraints (.constraints y))
         (maybe x)))

  Collection
  (conj [mc c]
    (either (or (or (= bottom-type c)
                    (and (some constraints (partial = c))
                         (maybe mc)))
                (and (= top-type c)
                     (maybe mc))
                (and (instance? MultiConstraint c)
                     (maybe (MultiConstraint (comp constraints
                                                   (.constraints c))))))
            (MultiConstraint (conj constraints c))))

  Constraint
  (extract-type-map [mc]
    (reduce (.constraints mc) nothing
            (fn [type-map c]
              (or (map type-map (partial restrict-types c))
                  (extract-type-map c)))))

  (update-sym [mc sym]
    (.constraints mc (map (.constraints mc)
                          (fn [c]
                            (update-sym c sym)))))

  (update-var [mc var]
    (.constraints mc (map (.constraints mc)
                          (fn [c]
                            (update-var c var)))))

  (format-path [_ n]
    (let [max-len (reduce (map constraints (fn [o]
                                             (count (.path o))))
                          n
                          (fn [max-len path-len]
                            (either (> max-len path-len)
                                    path-len)))
          paths (map constraints (fn [c]
                                   (format-path c (inc max-len))))]
      (-> paths
          (list-zipper empty-list)
          (map (fn [strs]
                 (to-str (interpose strs "    ")))))))

  (assertion-path [c]
    (format-path c 0))

  (update-sym [mc sym]
    (.constraints mc (map constraints (fn [c]
                                        (update-sym c sym)))))

  (update-var [mc var]
    (.constraints mc (map constraints (fn [c]
                                        (update-var c var)))))

  (update-path [mc file-name line-number]
    (.constraints mc (map (.constraints mc)
                          (fn [c]
                            (update-path c file-name line-number)))))

  (intersect [mc c]
    ;; TODO: refactor this before finishing
    (either (or (and (instance? MultiConstraint c)
                     (maybe (reduce (.constraints c) mc
                                    (fn [mc new-c]
                                      (either (= mc bottom-type)
                                              (intersect mc new-c))))))
                (= bottom-type (reduce (.constraints mc) c
                                       (fn [x y]
                                         (either (= bottom-type (intersect x y))
                                                 x))))
                (and (some constraints (partial = c))
                     (maybe mc)))
            (MultiConstraint (conj constraints c)))))

(deftype ResultConstraint [assertion]
  (assert (instance? ValueConstraint assertion))

  Stringable
  (string-list [_] (list "<ResultConstraint " (str assertion) ">"))

  Constraint
  (update-var [_ new-var]
    (ResultConstraint (update-var assertion new-var)))

  (update-sym [_ new-var]
    (ResultConstraint (update-sym assertion new-var)))

  (extract-type-map [_]
    (extract-type-map assertion))

  (intersect [rc c]
    (let [intersected (either (and (instance? ResultConstraint c)
                                   (maybe (intersect assertion (.assertion c))))
                              (intersect assertion c))]
      (either (= bottom-type intersected)
              (ResultConstraint intersected))))

  Eq
  (=* [x y]
    (and (or (and (instance? ResultConstraint y)
                  (=* assertion (.assertion y)))
             (=* assertion y))
         (maybe x))))

(deftype ItemsConstraint [items-constraints path sym var]
  (assert (instance? Vector items-constraints))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Collection
  (empty? [_]
    (every items-constraints empty?))

  Eq
  (=* [x y]
    (and (instance? ItemsConstraint y)
         (= items-constraints (.items-constraints y))
         (maybe x)))

  Stringable
  (string-list [_]
    (list "<ItemsConstraint " (str items-constraints) " " (str sym) ">"))

  Constraint
  (assertion-path [_]
    path)
  
  (extract-type-map [_]
    (maybe {ListType #{}
            VectorType #{}}))

  (extract-items-constraints [_]
    items-constraints)

  (format-path [x desired-len]
    (format-path* (cons ["Sequence from" ""] path)
                  desired-len))

  (intersect [ic c]
    (either (or (= ic c)
                (and (instance? ItemsConstraint c)
                     (let [constraints (map (zip-lists (.items-constraints ic) (.items-constraints c))
                                            (fn [[c1 c2]]
                                              (intersect c1 c2)))]
                       (or (some constraints (partial = bottom-type))
                           (maybe (.items-constraints ic (vec constraints)))))))
            (intersect (update-var c (.var ic)) ic))))

(def empty-items-constraint (ItemsConstraint [] empty-list nothing ""))

(deftype TypeConstraint [type-maps path type-sym sym var]
  (assert (instance? HashMap type-maps))
  (assert (instance? List path))
  (assert (instance? Tagged type-sym))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Stringable
  (string-list [_]
    (list "<TypeConstraint " (str type-sym) " " (str type-maps)
          " " (str sym) " '" (str var) "' " (str (either (first path) "")) ">" ))

  Eq
  (=* [x y]
    (and (instance? TypeConstraint y)
         (= type-maps (.type-maps y))
         (maybe x)))

  Constraint
  (assertion-path [_] path)

  (satisfied-by [c info]
    (flat-map (extract-type-map info)
              (fn [value-type-map]
                (and (< 0 (count value-type-map))
                     (every (keys value-type-map)
                            (partial get type-maps))))))

  (extract-type-map [_]
    (maybe type-maps))

  (format-path [x desired-len]
    (format-path* (cons [(str "Type '" type-sym "' from") ""]
                        path)
                  desired-len))

  (intersect [tc c]
    (either (or (= tc c)
                (flat-map (extract-type-map c)
                          (fn [type-map]
                            (or (and (instance? MultiConstraint c)
                                     (maybe (intersect c tc)))
                                (and (empty? (select-keys (.type-maps tc) (keys type-map)))
                                     (maybe (NoValues [c tc])))
                                (and (instance? TypeConstraint c)
                                     (maybe (.type-maps tc (filter-keys (.type-maps tc)
                                                                        (fn [k]
                                                                          (get type-map k))))))
                                (maybe (MultiConstraint [tc (update-var c (.var tc))])))))
                (and (instance? ItemsConstraint c)
                     (maybe (MultiConstraint [tc (update-var c (.var tc))]))))
            (intersect (update-var c (.var tc)) tc))))

(deftype FieldConstraint [field field-var path sym var]
  (assert (instance? Tagged field))
  (assert (instance? List path))
  (assert (instance? Maybe sym))

  Eq
  (=* [x y]
    (and (instance? FieldConstraint y)
         (= field (.field y))
         (maybe x)))

  Stringable
  (string-list [_]
    (list "<FieldConstraint " (str field) " " (str sym)
          ;; "\n" (str (interpose path "\n"))
          ">"))

  Constraint
  (assertion-path [_]
    path)

  (satisfied-by [c info]
    (let [type-map (extract-type-map info)]
      (or (flat-map type-map
                    (fn [type-map]
                      (first (seq (restrict-types c type-map)))))
          (and (= nothing type-map)
               (or (and (instance? FieldConstraint info)
                        (= (.field c) (.field info)))
                   (and (instance? MultiConstraint info)
                        (some (.constraints info)
                              (partial satisfied-by c))))))))

  (extract-type-map [_]
    nothing)

  (restrict-types [c type-map]
    (reduce (seq type-map) {}
            (fn [type-map [type-num field-set]]
              (either (map (field-set field)
                           (fn [_] (assoc type-map type-num field-set)))
                      type-map))))

  (format-path [x desired-len]
    (format-path* (cons [(str "Field '" (subs (str field) 1) "' required from") ""]
                        path)
                  desired-len))

  (intersect [fc c]
    (either (or (= fc c)
                (and (instance? FieldConstraint c)
                     (maybe (MultiConstraint [fc (update-var c (.var fc))])))
                (and (instance? ItemsConstraint c)
                     (maybe (NoValues [fc c])))
                (and (instance? TypeConstraint c)
                     (let [field (.field fc)]
                       (or (and (empty? (filter (vals (.type-maps c)) (fn [fields]
                                                                        (fields field))))
                                (maybe (NoValues [fc c])))
                           (maybe (MultiConstraint [fc (update-var c (.var fc))]))))))
            (intersect (update-var c (.var fc)) fc))))

(deftype StaticIntConstraint [val path sym var]
  (assert (instance? Integer val))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Stringable
  (string-list [_]
    (list "<StaticInt " (str val) ", " (str sym) ", " (str (either (first path) "")) ">"))

  Eq
  (=* [x y]
    (and (instance? StaticIntConstraint y)
         (= val (.val y))
         (maybe x)))

  Constraint
  (assertion-path [c]
    (.path c))

  (format-path [x desired-len]
    (format-path* (cons ["Integer from" ""] path)
                  desired-len))

  (extract-type-map [_]
    (maybe {IntegerType #{}}))

  (intersect [slc c]
    (either (or (= slc c)
                (and (or (instance? ItemsConstraint c)
                         (instance? StaticIntConstraint c)
                         (instance? FieldConstraint c))
                     (maybe (NoValues [slc c]))))
            (intersect (update-var c (.var slc)) slc))))

(deftype MinValue [min path sym var]
  (assert (instance? Integer min))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Stringable
  (string-list [_]
    (list "<MinValue " (str min) " " (str sym) " '" (str var) "' " (str (either (first path) "")) ">" ))

  Eq
  (=* [x y]
    (and (instance? MinValue y)
         (= min (.min y))
         (maybe x)))

  Constraint
  (assertion-path [_]
    path)

  (extract-type-map [_]
    (maybe {IntegerType #{}}))

  (format-path [x desired-len]
    (format-path* (cons [(str "Min value of " min " from") ""] path)
                  desired-len))

  (intersect [mvc c]
    (either (or (= mvc c)
                (and (or (instance? ItemsConstraint c)
                         (instance? FieldConstraint c))
                     (maybe (NoValues [mvc c])))
                (and (instance? MinValue c)
                     (maybe (.min mvc (either (< min (.min c))
                                              (.min c)))))
                (and (instance? StaticIntConstraint c)
                     (or (and (< min (.val c))
                              (maybe (NoValues [mvc c])))
                         (maybe c))))
            (intersect (update-var c (.var mvc)) mvc))))

(deftype MaxValue [max path sym var]
  (assert (instance? Integer max))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Stringable
  (string-list [_]
    (list "<MaxValue " (str max) " " (str sym) " '" (str var) "' " (str (either (first path) "")) ">" ))

  Eq
  (=* [x y]
    (and (instance? MaxValue y)
         (= max (.max y))
         (maybe x)))

  Constraint
  (assertion-path [_]
    path)

  (extract-type-map [_]
    (maybe {IntegerType #{}}))

  (format-path [x desired-len]
    (format-path* (cons [(str "Max value of " max " from") ""] path)
                  desired-len))

  (intersect [mvc c]
    (either (or (= mvc c)
                (and (or (instance? ItemsConstraint c)
                         (instance? FieldConstraint c))
                     (maybe (NoValues [mvc c])))
                (and (instance? MaxValue c)
                     (maybe (.max mvc (either (< max (.max c))
                                              (.max c)))))
                (and (instance? StaticIntConstraint c)
                     (or (and (< max (.val c))
                              (maybe (NoValues [mvc c])))
                         (maybe c))))
            (intersect (update-var c (.var mvc)) mvc))))

(deftype InnerConstraint [inner path sym var]
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Eq
  (=* [x y]
    (and (instance? InnerConstraint y)
         (= inner (.inner y))
         (maybe x)))

  Stringable
  (string-list [_]
    (list "<InnerConstraint " (str inner) ", " (str sym) ", " (str (either (first path) "")) ">"))

  Constraint
  (extract-type-map [_]
    nothing)

  (assertion-path [_]
    path)

  (format-path [x desired-len]
    (format-path* (cons ["Contents restriction from" ""] path)
                  desired-len))

  (intersect [ic c]
    ;; TODO: compose InnerConstraints inside MultiConstraint
    ;; TODO: compose InnerConstraints with each ItemsConstraint
    (either (or (= ic c)
                (and (or (instance? StaticIntConstraint c)
                         (instance? MaxValue c))
                     (maybe (NoValues [ic c])))
                (and (instance? InnerConstraint c)
                     (let [new-c (intersect inner (.inner c))]
                       (or (= bottom-type new-c)
                           (maybe (.inner ic new-c)))))
                (and (instance? ItemsConstraint c)
                     (let [constraints (map (.items-constraints c)
                                            (fn [c2]
                                              (intersect inner c2)))]
                       (or (some constraints (partial = bottom-type))
                           (maybe (MultiConstraint [ic (update-var c (.var ic))])))))
                (and (instance? FieldConstraint c)
                     (maybe (MultiConstraint [ic (update-var c (.var ic))])))
                (and (instance? TypeConstraint c)
                     (let [new-type-map (dissoc (.type-maps c) IntegerType SymbolType)]
                       ;; TODO: incomplete. need to account for Strings
                       ;; also account for Fn's
                       (or (and (empty? new-type-map)
                                (maybe (NoValues [ic c])))
                           (maybe (MultiConstraint [ic (-> c
                                                           (.type-maps new-type-map)
                                                           (update-var (.var ic)))]))))))
            (intersect (update-var c (.var ic)) ic))))

(deftype StaticLengthConstraint [length path sym var]
  (assert (instance? Integer length))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Eq
  (=* [x y]
    (and (instance? StaticLengthConstraint y)
         (= (.length x) (.length y))
         (maybe x)))

  Stringable
  (string-list [c]
    (list "<StaticLengthConstraint " (str (.length c)) " " (str (.sym c)) "\n"
          (to-str (interpose (seq (.path c)) "\n")) ">"))

  Constraint
  (assertion-path [c]
    (.path c))

  (format-path [x desired-len]
    (format-path* (cons [(str "Length from") ""]
                          (.path x))
                    desired-len))

  (extract-type-map [_]
    (maybe {ListType #{}
            VectorType #{}}))

  (intersect [slc c]
    (either (or (= slc c)
                (and (instance? ItemsConstraint c)
                     (or (and (< (.length slc) (count (.items-constraints c)))
                              (maybe (NoValues [slc c])))
                         (maybe (MultiConstraint [slc c]))))
                (and (instance? InnerConstraint c)
                     (maybe (MultiConstraint [slc c])))
                (and (or (instance? StaticIntConstraint c)
                         (instance? FieldConstraint c)
                         (instance? MaxValue c))
                     (maybe (NoValues [slc c]))))
            (intersect (update-var c (.var slc)) slc))))

(deftype StaticStrConstraint [val path sym var]
  (assert (instance? String val))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Stringable
  (string-list [_]
    (list "<StaticString " (str val) ", " (str sym) ", " (str (either (first path) "")) ">"))

  Eq
  (=* [x y]
    (and (instance? StaticStrConstraint y)
         (= val (.val y))
         (maybe x)))

  Constraint
  (assertion-path [c]
    (.path c))

  (intersect [slc c]
    (either (or (= slc c)
                (and (or (instance? InnerConstraint c)
                         (instance? StaticLengthConstraint c))
                     (maybe (MultiConstraint [slc c])))
                (and (instance? ItemsConstraint c)
                     (or (and (< (count slc) (count (.items-constraints c)))
                              (maybe (NoValues [slc c])))
                         (maybe (MultiConstraint [slc c]))))
                (and (or (instance? StaticIntConstraint c)
                         (instance? FieldConstraint c)
                         (instance? MaxValue c)
                         (instance? StaticStrConstraint c))
                     (maybe (NoValues [slc c]))))
            (intersect (update-var c (.var slc)) slc))))

(def StaticConstraints (comp StaticLengthConstraint
                             StaticStrConstraint
                             StaticIntConstraint))

(deftype ContentsConstraint [constraints path sym var]
  (assert (instance? Vector constraints))
  (assert (instance? List path))
  (assert (instance? Maybe sym))
  (assert (instance? String var))

  Eq
  (=* [x y]
    (and (instance? ContentsConstraint y)
         (= (count constraints) (count (.constraints y)))
         (every (.constraints y) (fn [c]
                                   (some constraints (partial = c))))
         (maybe x)))

  Stringable
  (string-list [_]
    (list "<ContentsConstraint " (str constraints) ", " (str sym) ", " (str (either (first path) "")) ">"))

  Constraint
  (extract-type-map [_]
    nothing)

  (assertion-path [_]
    path)

  (format-path [x desired-len]
    (format-path* (cons ["Contents from" ""] path)
                  desired-len))

  (intersect [cc c]
    (either (or (= cc c)
                (and (instance? InnerConstraint c)
                     (or (= bottom-type (reduce (.constraints cc) (.inner c)
                                                (fn [inner y]
                                                  (either (= bottom-type (intersect inner y))
                                                          inner))))
                         (maybe (MultiConstraint [cc (update-var c (.var cc))]))))
                (and (or (instance? StaticLengthConstraint c)
                         (instance? ItemsConstraint c)
                         (instance? FieldConstraint c)
                         (instance? TypeConstraint c))
                     (maybe (MultiConstraint [cc (update-var c (.var cc))])))
                (and (or (instance? StaticIntConstraint c)
                         (instance? MaxValue c)
                         (instance? StaticStrConstraint c))
                     (maybe (NoValues [cc c])))
                (and (instance? ContentsConstraint c)
                     (maybe (.constraints cc
                                          (comp constraints
                                                (for [x (.constraints c)
                                                      :when-not (some constraints (partial = x))]
                                                  x))))))
            (intersect (update-var c (.var cc)) cc))))

(def SymbolConstraints (comp TypeConstraint
                             MaxValue
                             InnerConstraint
                             StaticConstraints
                             ItemsConstraint
                             ContentsConstraint
                             FieldConstraint))

(extend-type SymbolConstraints
  Constraint
  (update-path [constraint file-name line-number]
    (let [file-name (either (and (= file-name 'core)
                                 (maybe "$TOCCATA_DIR/core.toc"))
                            file-name)
          new-path (either (for [[top-file top-line] (first (.path constraint))
                                 _ (and (= top-line line-number)
                                        (= top-file file-name))]
                             (.path constraint))
                           (cons [file-name line-number] (.path constraint)))]
      (either (and (= "" file-name)
                   (maybe constraint))
              (.path constraint new-path))))

  (update-sym [tc new-sym]
    (.sym tc (maybe new-sym)))

  (update-var [tc new-var]
    (.var tc new-var)))

(extend-type ItemsConstraint
  Constraint
  (update-path [constraint file-name line-number]
    (let [file-name (either (and (= file-name 'core)
                                 (maybe "$TOCCATA_DIR/core.toc"))
                            file-name)
          new-path (either (for [[top-file top-line] (first (.path constraint))
                                 _ (and (= top-line line-number)
                                        (= top-file file-name))]
                             (.path constraint))
                           (cons [file-name line-number] (.path constraint)))]
      (ItemsConstraint (map (.items-constraints constraint)
                            (fn [constraints]
                              (update-path constraints file-name line-number)))
                       new-path
                       (.sym constraint) (.var constraint)))))

(def ValueConstraint (comp SymbolConstraints
                           MultiConstraint
                           AllValues))

(def Constraints (comp AllValues
                       NoValues
                       MultiConstraint
                       SymbolConstraints
                       ResultConstraint))

(extend-type Constraints
  Constraint
  (extract-inner-constraint [_]
    top-type))

(extend-type MultiConstraint
  Constraint
  (satisfied-by [c info]
    (let [type-map (extract-type-map c)]
      (and (or (= nothing type-map)
               (flat-map type-map
                         (fn [type-map]
                           (satisfied-by (TypeConstraint type-map empty-list (symbol "<unknown>") nothing "")
                                         info))))
           (every (.constraints c)
                  (fn [inner-c]
                    (satisfied-by inner-c info))))))

  (extract-items-constraints [mc]
    (either (-> (.constraints mc)
                (some (partial instance? ItemsConstraint))
                (map .items-constraints))
            []))

  (extract-inner-constraint [c]
    (either (some (.constraints c) (fn [c]
                                     (instance? InnerConstraint c)))
            top-type)))

(extend-type InnerConstraint
  Constraint
  (update-path [constraint file-name line-number]
    (let [file-name (either (and (= file-name 'core)
                                 (maybe "$TOCCATA_DIR/core.toc"))
                            file-name)
          new-path (either (for [[top-file top-line] (first (.path constraint))
                                 _ (and (= top-line line-number)
                                        (= top-file file-name))]
                             (.path constraint))
                           (cons [file-name line-number] (.path constraint)))]
      (-> constraint
          (.inner (update-path (.inner constraint) file-name line-number))
          (.path new-path))))

  (extract-inner-constraint [c]
    c))

(defn conflicting-assertions [failing-constraint file-name line-number]
  (apply print-err (list* "Conflicting assertions"
                           (either (= "" file-name)
                                   (str "at " file-name ": " line-number))
                           "\n"
                           (format-path failing-constraint 0)))
  nothing)

(defn compose-constraints [file-name line-number x y]
  (assert (instance? Constraints x))
  (assert (instance? Constraints y))

  (let [z (intersect x y)
        file-name (either (and (= file-name 'core)
                               (maybe "$TOCCATA_DIR/core.toc"))
                          file-name)]
    (either (and (= bottom-type z)
                 (let [z (either (and (< 1 (count (.constraints z)))
                                      (maybe z))
                                 (NoValues [x y]))]
                   (maybe (conflicting-assertions z file-name line-number))))
            (maybe z))))
